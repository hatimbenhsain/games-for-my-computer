title: NPCTesterStart
tags: 
colorID: 0
position: -205,-2
---
<<declare $positiveanswer = false as bool>>
    <<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
    <<ChangeSoundtoPlayer YarnManager>>
Nour: Greetings my dude.
    <<MoveBox1 YarnManager>>
    <<ChangeAvatartoCharacter YarnManager>>
NPC: Whoa. Dude.
NPC: I’ve never seen someone flop like that before. 
    <<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
-> You think that I’m flopping??? Like a fish??
-> It feels awesome!!!!!! You should try it!!!
    <<set $positiveanswer to true>>
<<if $positiveanswer>>
Nour: It feels awesome!!!!!! You should try it!!!
    <<MoveBox1 YarnManager>>
    <<ChangeAvatartoCharacter YarnManager>>
NPC: I don’t think I’m up for it, I messed up my back eating watermelon.
NPC: Or was it when I was trying to arm wrestle my feet?
NPC: I can't remember.
    <<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
Nour: Bummer...See ya later!    
<<else>>
Nour: You think that I’m flopping??? Like a fish??
    <<MoveBox1 YarnManager>>
    <<ChangeAvatartoCharacter YarnManager>>
NPC: Like the most beautiful fish to ever venture out of the ocean!
    <<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
-> Thank you 
    <<MoveBox1 YarnManager>>
    <<ChangeAvatartoCharacter YarnManager>>
    NPC: No problem dude! Flop on!
-> Okay...
    <<MoveBox1 YarnManager>>
    <<ChangeAvatartoCharacter YarnManager>>
    NPC: Just trying to appreciate a good flop, my bad…
<<endif>>
===
title: Complimentee1part1
tags: 
colorID: 0
position: -81,-315
---
<<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
    <<ChangeSoundtoPlayer YarnManager>>

Nour: Hi! How's it, uh.. hanging?
    <<ChangeAvatartoCharacter YarnManager>>
    <<ChangeSoundtoCharacter YarnManager>>

NPC: *Sigh*
NPC: I've been better.
NPC: Can I help you with anything?
    <<ChangeAvatartoPlayer YarnManager>>
Nour: N-no! But maybe... I can help?
-> Compliment them.
    <<ChangeAvatartoPlayer YarnManager>>
    Nour: Do you mind if I... compliment you?
    <<ChangeAvatartoCharacter YarnManager>>
    NPC: Sure?
    <<ChangeStartNode ArmGuy Complimentee1part2>>
    <<declare $compliment="You're beautiful.">>
    <<ComplimentStart GameManager>>
-> Leave them alone.
    <<ChangeAvatartoPlayer YarnManager>>
    Nour: Uuuh. Sorry. Nevermind, bye!!!
<<stop>>
===
title: Complimentee1part2
tags: 
colorID: 0
position: 131,-316
---
<<MoveBox2 YarnManager>>
<<ChangeAvatartoPlayer YarnManager>>
Nour: {$compliment}
<<ChangeAvatartoCharacter YarnManager>>
NPC: Wh... woah. Thank you.
<<ChangeAvatartoPlayer YarnManager>>
Nour: Hey, uuuh... Don't sweat it.
<<ChangeStartNode ArmGuy Complimentee1part3>>
===
title: Complimentee1part3
tags: 
colorID: 0
position: 340,-317
---
<<MoveBox2 YarnManager>>
    <<ChangeAvatartoPlayer YarnManager>>
Nour: So umm... was that good?
    <<ChangeAvatartoCharacter YarnManager>>
NPC: Yeah! Thank you for everything :)
<<stop>>
===
title: Rain1
position: -126,183
---
<<MoveBox2  YarnManager>>
Rain: Hello little Cockroach...
Nour: Whu-- Cockroach? Is that what I look like? I don't-- who's talking?!
Rain: We see you have found yourself in quite the predicament.
Nour: I... Yeah!!! Like, who was that kid? Do they even go here?
Nour: And why was she kissing them?? What do they have that I don't have??? Why do I look like a fish???? Was that a remix?
Rain: Worry not. All you need to do is press *SPACE* and *WASD* and soon you will be able to reach your destination.
Nour: *SPACE* and *WASD*? What does that even mean???
Rain: And talk to people with E.
Rain: Flounder on.
<<EndRain  RainThatTalksToYou>>
Nour: Wh-- Wait! Who are you? What is going on??? I...
Nour: I guess I'lll. AGGHHH!!
Nour: I should probably head to the hospital. Maybe they can fix me up...
Nour: It's that red building at the end of the street. I guess... Let's see if I can reach it.
Nour: *SPACE* and *WASD*. Ok. And E to talk.
===
title: Rain2
position: 85,183
---
<<MoveBox2  YarnManager>>
Rain: And so the cockroach regains its legs -- yet they are changed.
Nour: This is... a goat leg?!
Rain: They are Nour legs.
Nour: Are you... real?
Rain: Do you not feel me in your fur and on your hoof and in your mouth and on your skin?
Nour: I guess but...
Rain: Take heart. You will experience untold freedom. All you need to do is press *WASD*...
Rain: And if you desire to return to your roots, you can always press *CTRL*.
Rain: Gallop forth.
<<EndRain  RainThatTalksToYou>>
Nour: ...Thank you? I'll try that.
Nour: *WASD* and *CTRL*.... Okay.
Nour: The doctor said I should go to a "park"... I think there should be one behind that bridge over there.
Nour: And he said I should try complimenting some people - whatever that means... Let's try that I guess.
===
title: Rain3
position: 299,183
---
<<MoveBox2  YarnManager>>
<<StartRain  RainThatTalksToYou>>
Nour: I feel it again. Another change is coming, isn't it?
Rain: You have raised these folks' spirits up. Now it is your turn to ascend.
<<Metamorphosis PlayerController Wing>>
<<wait 2>>
Rain: And then, the cockroach found it had wings.
Nour: WOAH! I've always wanted wings!
Nour: ...Huh.
Nour: It's odd. I've always been scared of telling people how I really feel. And complimenting felt so strange because I thought I would be seen as disingenuous.
Nour: But it genuinely felt so good. And no one was mad or freaked out! In a way I kind of... impacted their lives in a little way?
Nour: This whole night I've been so stressed about this whole situation. But now it feels like things are looking up.
Rain: And they will continue on. Now, soar! 
Rain: You may do so by pressing *SPACE*.
<<EndRain  RainThatTalksToYou>>
Nour: Ok... I'm ready. Here I go.
<<wait  1>>
<<jump AgentPostCompliments>>
===
title: Rain4
position: 504,181
---
<<MoveBox2  YarnManager>>
Nour: I know what has to be done.
<<Metamorphosis PlayerController Rocket>>
<<wait 2>>
Rain: And finally, the cockroach gained its antenna.
Nour: ...Cool? It's weird though. These usually do something.
Rain: Everything has its purpose. But it's also okay to be purposeless.
Nour: I feel really close to you right now. Do you wanna hang out sometime?
Rain: We will see each other again, but in a different form than this one.
Nour: Oh okay I mean we don't have to if you don't wanna--
Rain: For now, you may press the *RIGHT MOUSE BUTTON*, and a new world will open to you.
Nour: Okay. Got it. *RIGHT MOUSE BUTTON*.
Nour: Umm... I'll.. miss you?
Rain: We will always be with you. Rocket onwards.
<<EndRain  RainThatTalksToYou>>
Nour: Rocket onwards... Huh. Maybe I can reach that big thing in the sky now.
===
title: GreenHair1
tags: 
colorID: 0
position: 7,-5
---
<<MoveBox2 YarnManager>>
<<if $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
Nour: Hey... there.
Green Hair: What need you from me?
Nour: What? Um, nothing! Why are you assuming I need something from you?
Green Hair: *Sigh* All people always to me come looking for help or service of sort.
Nour: Well, I...
-> Compliment them.
    Nour: I just wanna compliment you! Would that be okay??
    Green Hair: Oh? Why not, suppose I.
    <<ChangeStartNode GreenHair GreenHair2>>
    <<ComplimentStart GameManager>>
-> Something else.
    Nour: Actually... Do you have 5 dollars?
    Green Hair: Of course... Here you go.
    Nour: I have gained 5 dollars!
<<stop>>
<<else>>
    //PUT ONE LINER HERE
Green Hair: Isn't park this wonderful?
Nour: I guess?
Green Hair: Want you hear more about it?
->  Sure?
    Green Hair: It was built in 1424 by a great woman known as Purple Hair...
    Nour: And this was your ancestor?
    Green Hair: What? No?
    Nour: Sorry please go on.
    Green Hair: Yes... As was being said built it was after generous donation from Committee of Green-ish Spaces and Fountain Promotion of 1 gazillion simoleons!
    Nour: Wow... that's a pretty big number.
    Green Hair: Actually fairly insiginificant was at the time. Deflation, inflation, stagflation, etc. into account, it would today 4 bucks be.
    Nour: That's my monthly allowance!
    Green Hair: Yes... Anyregardless. Purple Hair decided build it with in mind those who enjoy seeing and being seen...
    Green Hair: ...as a safe space to each other come up and nice things say to each other!
    Nour: What a noble premise.
    Green Hair: Indeed!
->  No?
    Nour: Maybe another time?
    Green Hair: Very well.
<<endif>>
===
title: GreenHair2
tags: 
colorID: 0
position: 218,-5
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
Green Hair: ...!
Green Hair: For ever no one has given me tender words such.
<<Complimented  GreenHair>>
Green Hair: Thank you my sweet. Keep you in my heart will I.
Nour: No worries...!
<<ChangeStartNode GreenHair GreenHair3>>
<<jump PostCompliment>>
===
title: GreenHair3
tags: 
colorID: 0
position: 433,-1
---
<<MoveBox2 YarnManager>>
Nour: Anyone asked you for a service recently?
Green Hair: Yes... But I found newfound joy for the helping!
Green Hair: And now, better boundaries... I have those!
Nour: That's really amazing.
<<stop>>
===
title: Tabitha1
position: 785,252
---
<<MoveBox2 YarnManager>>
<<if $TabithaComplimented is true>>
    // This part is meant for what happens if you talk to Tabitha after museum game after she's been previously complimented
    // This does NOT currently work because variables aren't saved between scenes and IDT it will be implemented but maybe
    <<Complimented  Tabitha>>
    <<ChangeStartNode Tabitha Tabitha3>>
    <<jump Tabitha3>>
<<elseif $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
    Nour: Hello tall flower.
    Tabitha: ...Don't tell me...
    Nour: Oh? I dunnu I...
    Tabitha: You're here to take one of my flowers?
    Nour: I definitely wasn't thinking that.
    Tabitha: That's what they all say...
    Nour: Do people take from you often?
    Tabitha: Actually all of the time, and I only bloom so often.
    Nour: Hmmm...
    -> Compliment them.
        Nour: I just wanna compliment you! Would that be okay??
        Tabitha: Sure okay!
        <<ChangeStartNode Tabitha Tabitha2>>
        <<ComplimentStart GameManager>>
    -> Something else.
        Nour: Can I just stare at your flowers for a second?
        Tabitha: That's strange.
        Nour: Okay....nevermind....
    <<stop>>
<<else>>
    //PUT ONE LINER HERE
<<endif>>
===
title: Tabitha2
position: 1005,255
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
Tabitha: ...!
Tabitha: ...feeling something...eep!!
<<Complimented  Tabitha>>
Tabitha: I'm blossoming~! Thank you, this is so rare.
Nour: Happy blossoming!
<<set $TabithaComplimented = true>>
<<ChangeStartNode Tabitha Tabitha3>>
<<jump PostCompliment>>
===
title: Tabitha3
position: 1224,257
---
<<MoveBox2 YarnManager>>
Nour: Still blossoming?
Tabitha: Yeah..*ahem*...this is awkward to do in public...but I'm so grateful!
Nour: Keep up the good work!
<<stop>>
===
title: KissingCouple1
position: 350,727
---
<<MoveBox2 YarnManager>>
<<if $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
    Nour: Whoa you are going at it!
    Kissing Couple: We so are!
    -> Compliment them.
        Nour: I wanna tell you both some nice things!
        <<ChangeStartNode KissingCouple KissingCouple2>>
        <<ComplimentStart GameManager>>
    -> Something else.
        Kissing Couple: I hope one day you'll find a love as pure as ours!
        Nour: See ya later love birds.
    <<stop>>
<<else>>
    //PUT ONE LINER HERE
<<endif>>
===
title: KissingCouple2
position: 561,726
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
<<Complimented  KissingCouple>>
Kissing Couple: Whoa!!! 
Nour: I didn't know human bodies could do that! 
Kissing Couple: We're gonna stay like this forever.
Nour: I hope you set the world record.
<<ChangeStartNode KissingCouple KissingCouple3>>
<<jump PostCompliment>>
===
title: KissingCouple3
position: 787,729
---
<<MoveBox2 YarnManager>>
Kissing Couple: *smack slurp smack slurp slurp slurp*
<<stop>>
===

title: ShoeBaby1
position: -503,697
---
<<MoveBox2 YarnManager>>
<<if $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
    Shoe Baby: *ooomph*! Can you be a bit more careful...I'm sorta unnoticeable...
    Nour: No, I meant to say hello. So, Hi!
    Shoe Baby: Unbelievable...so do you come...eh...around this area often?
    -> Yeah, just to say hi to you! 
        Nour: Yeah, just to say hi to you! 
        -> Compliment them!
            Nour: Compliment incoming!!
            <<ChangeStartNode ShoeBaby ShoeBaby2>>
            <<ComplimentStart GameManager>>
        -> I didn't...I just noticed you from *this* close.
            Shoe Baby: I don't really go anywhere different.
            Nour: Is it because of your size?
            Shoe Baby: ...
            Nour:...
    -> This is my first time.
        Shoe Baby: saying hi is pretty cool still.
        Nour: talk to you later!
    <<stop>>
<<else>>
    //PUT ONE LINER HERE
<<endif>>
===

title: ShoeBaby2
position: -279,697
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
Shoe Baby: ...!
Shoe Baby: Hahahaha that's tickly!!
<<Complimented  ShoeBaby>>
Shoe Baby: Thank you so for even noticing me in the first place! Wowie.
Nour: Anytime.
<<ChangeStartNode ShoeBaby ShoeBaby3>>
<<jump PostCompliment>>
===

title: ShoeBaby3
position: -61,695
---
<<MoveBox2 YarnManager>>
Nour: Feeling noticeable?
Shoe Baby: Yipee!
Nour:...
Nour: Yipee!
<<stop>>
===
title: Naut1
position: -377,535
---
<<MoveBox2 YarnManager>>
<<if $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
    Nour: Cool shell... nice to meet you!
    Naut: I used to be energetic like you...
    -> What happened?
        Nour: What happened?
        Naut: Ever since that mysterious archecture when up in the sky I haven't been able to get a lick of sleep.
        Nour: Why not?
        Naut: It has completely destroyed the skyline's feng shui.
        Nour: That's no reason to lose sleep!
        Naut: Oh yeah?
        -> Compliment them!
            Nour: Thinking up an awesome compliment for you!
            <<ChangeStartNode Naut Naut2>>
            <<ComplimentStart GameManager>>
        -> Nah
            Nour: Hope you get some sleep dude.
            Naut: Me too...
    -> You still can be!
        Naut: Am I supposed to just decide not to be tired?
            -> Compliment them!
                Nour: Thinking up an awesome compliment for you!
                <<ChangeStartNode Naut Naut2>>
                <<ComplimentStart GameManager>>
            -> Nah
                Nour: Hope you get some sleep dude.
                Naut: Me too...
    <<stop>>
<<else>>
    //PUT ONE LINER HERE
<<endif>>
===
title: Naut2
position: -158,526
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
<<Complimented  Naut>>
Naut: Wow! I'm feeling a little energized! 
Nour: You look it!
<<ChangeStartNode Naut Naut3>>
<<jump PostCompliment>>
===
title: Naut3
position: 48,524
---
<<MoveBox2 YarnManager>>
Naut: I think I might go for a run!
Nour: Sounds fun!
<<stop>>
===

title: Thwomp1
position: -67,373
---
<<MoveBox2 YarnManager>>
<<if $mcState is "Leg">> // AKA if nour can compliment right now //character states: Fish, Leg, Wing, Rocket
    Thwomp: I'm looking pretty flat huh?
    Nour: I guess?
    Thwomp: As flat as you right?
    Nour: Do you want to be?
    Nour: I feel like I should be...
    -> Compliment them.
        Nour: I'm thinking up some real nice things to say!
        <<ChangeStartNode Thwomp Thwomp2>>
        <<ComplimentStart GameManager>>
    -> Nah.
        Nour: See you around dude.    
    <<stop>>
<<else>>
    //PUT ONE LINER HERE
<<endif>>
===

title: Thwomp2
position: 147,373
---
<<MoveBox2 YarnManager>>
Nour: {$compliment}
<<Complimented  Thwomp>>
Thwomp: WHOA!!! 
Thwomp: Wow I feel like I can really let loose and be myself now. 
Nour: Looking good!
<<ChangeStartNode Thwomp Thwomp3>>
<<jump PostCompliment>>
===
title: Thwomp3
position: 358,380
---
<<MoveBox2 YarnManager>>
Thwomp: Taking up space! 
<<stop>>
===
title: Farez
position: 534,544
---
<<MoveBox2 YarnManager>>
Nour: How's your day so far?
Farez: Hello! Well...first I went to the grocery store. Then I wrote in my novel. Then I...
Nour: Sounds like a lot!
Farez: Yes. So much has happened. Then my grandma needed a new lightbulb. Then...
Nour: Maybe you can tell me how you're doing instead?
Farez: How I'm doing?? Ok, well...It all started when I was 6. That was when I first started feeling...
Nour: You don't remember feeling before 6?
Farez: ...I remember specifically noting in my journal that at the age of 6 was the first time I fully felt sorrow...
Farez: which is the first kind of feeling that really counts. Then when I was 20 I...
Nour: You have a lot to say...
Farez: Well some people do seem to say that about me...and then when I hear that I start to feel...
Nour: ...Some kind of a feeling?
Farez: Some kind of a feeling. Correct.
Nour: Feelings so happen.
<<stop>>
===
title: AgentPostCompliments
position: 831,-31
---
<<PlaceInterlocutor DialogueSystem Agent>>
Agent: Whoa there champ! What is going on with your body?
Nour: I’m not sure! It’s just what’s been happening today!
Agent: Wow champ, your wings look pretty groovy.
Nour: My name is actually Nour, but yeah I guess it’s pretty cool.
Agent: You might be able to ascend the stairs to that big building over there with the UFO…
Nour: Probably?
Agent: Can I be honest with you champ? I love what you got going on here.
Agent: Might I interest you in the opportunity of a lifetime?
Nour: Perhaps…
Agent: You may not know this, as being covert is one of my strengths, but I’m a secret agent.
Nour: I had no idea! What kind of secret agent?
Agent: I work for BRAG, Big Righteous Agency Galatic. They’re undergoing a corporate rebrand, but I’m sure you’ve heard of them.
Nour: I definitely have not.
Agent: You're hired! Use those wings to head up the stairs and there should be a package waiting for you.
Agent: I'll be in touch!
<<DeleteInterlocutor DialogueSystem>>
===
title: PostCompliment
position: 831,-31
---
<<wait 0.5>>
<<if $complimentValue is 1>>
    <<jump PostCompliment1>>
<<elseif $complimentValue is 2>>
    <<jump PostCompliment2>>
<<elseif $complimentValue is 3>>
    <<jump PostCompliment3>>
<<endif>>
===
title: PostCompliment1
position: 831,-31
---
Nour: Huh... That felt odd. But also nice?
Nour: I wonder if anything will happen if I compliment more people.
===
title: PostCompliment2
position: 831,-31
---
Nour: Wow, I'm feeling like the words are just cascading out of my mouth!
Nour: Is this what the flow state is like?
===
title: PostCompliment3
position: 831,-31
---
Nour: This feels so great!!
Nour: I feel like something amazing will happen if I compliment just one more person...!
===
title: Cecile1
position: 831,-31
---
<<if $mcState is "Fish">>
    <<if visited("Cecile1") is false>>
        Cecile: HEY! Do you know if there's any bathroom around???
        Nour: Uuuh, not that I --
        Cecile: I REALLY need to pee!
        Nour: Ok ok! Um... I don't know, why don't you just do it in this alleyway?
        Cecile: Oh no, not again...
        Nour: Not again what?
        Cecile: It's a REALLY long story that I would rather not get into right now because I REALLY need to pee and I CANNOT embarass myself again!
        Nour: Ok! I'm sorry, I hope you figure it out!
    <<else>>
        Cecile: PLEASE let me know if you find out about any public bathroom anywhere!!!!
        Nour: Sure!!!
    <<endif>>
<<elseif $mcState is "Leg">>
    <<if visited("Cecile1") is false>>
        Cecile: HEY! Do you know if there's any bathroom around???
        Nour: So uh, I'm actually heading to this park right now. Maybe they..?
        Cecile: OH OK THAT'S TRUE THEY PROBABLY HAVE A PUBLIC BATHROOM.
        Nour: Yeah..!
        Cecile: ...
        Nour: So are you going or --
        Cecile: YOU KNOW WHEN YOU FEEL LIKE IF YOU DO ANY SMALL MOVEMENT YOU MIGHT PISS YOURSELF?
        Nour: Yeah...
        Cecile: I JUST NEED TO REGAIN MY COMPOSURE.
        Nour: Ok good luck...! I'll see you there maybe.
        Cecile: OK.
    <<else>>
        Cecile: I'LL SEE YOU AT THE PARK PLEASE STOP TALKING TO OR WAITING FOR ME IT'S MAKING IT WORSE.
        Nour: Ok!!!
    <<endif>>
<<elseif $mcState is "Wing">>
    Cecile: WHAT DO YOU MEAN CLOSED FOR MAINTENANCE.
<<elseif $mcState is "Rocket">>
    Nour: Did you--
    Cecile: I DID.
    Nour: You mean--
    Cecile: I PEED MYSELF.
    Nour: But you look like you still--
    Cecile: I DRANK TOO MUCH WATER.
<<endif>>
===
title: Blanche1
position: 831,-31
---
<<if visited("Blanche1") is false>>
    Ms. Mouse: Hello Nour... You've found me.
    Nour: Oh hello Ms. Mouse... (oh my god)
    Ms. Mouse: Ms. Mouse? That's not who I am.
    Nour: ...? Yes you are? You teach us algebra...
    Ms. Mouse?: No, no, no... Ms. Mouse has slightly longer left whiskers and wears glasses. I am Blanche the flying mouse, protector of the innocent and the downtrodden.
    Nour: Oh.
    Nour: Sure I guess.
    Blanche: ;).
<<else>>
    Blanche: You look fairly downtrodden to me.
    Nour: Oh I'm fine really.
    Nour: I mean I was really struggling earlier but now I have all these cool new abilities..!
    Blanche: But I can still sense a deep sadness in your heart.
    Nour: ...
    Blanche: Do you want to talk about it?
    Nour: It's OK Ms. Blanche I just have to deal with some stuff.
    Blanche: Are you sure?
    Nour: ...
    <<ChangeStartNode Blanche Blanche2>>
<<endif>>
===
title: Blanche2
position: 831,-31
---
Nour: What are you doing on this rooftop anyway?
Blanche: What are you doing avoiding your feelings?
Nour: What??? I'm not-- I was going to ask her out, OK!!! But she kissed Fedora!!!
Blanche: Hmm...
Nour: I'm-- sorry, this might be inappropriate given you teach us algebra and all.
Blanche: Again, not your teacher.
Nour: ...Sure.
<<ChangeStartNode Blanche Blanche3>>
===
title: Blanche3
position: 831,-31
---
Blanche: You know, I've also faced rejection, once upon a time...
Nour: Really??? I mean, I can't believe... Wow.
Blanche: Yes, there was this scarlet anteater...
Blanche: ...
Blanche: Anyway, that's a story for another time.
Nour: But you said I shouldn't avoid --
Blanche: Anyway.
<<ChangeStartNode Blanche Blanche3>>
===
title: Blanche4
position: 831,-31
---
<<if visited("Blanche4") is false>>
    Blanche: My point is, there are more gems this world has to offer.
    Nour: ...Gems?
    Blanche: Yes. Look at all the gems I have liberated tonight!
    Nour: ...Liberated?
    Nour: I thought you were a protector of the innocent and the downtrodden?
    Blanche: Exactly. And these gems were miserable before I liberated them.
    Nour: Wow...
<<else>>
    Nour: I hope I can be more like you one day, Ms. Blanche.
    Blanche: No, no, no. You will be something different entirely, my weanling.
    Nour: Thank you.
<<endif>>
===
title: Igor1
position: 831,-31
---
<<if $mcState is "Fish">>
    Igor: Hi... Nour?
    Nour: Yeah, it's me. Hi Igor.
    Igor: You look different.
    Nour: Yeah.
    Igor: Do you wanna talk about it?
    Nour: Not really.
    <<ChangeStartNode Igor Igor2>>
<<else>>
    Nour: Hi Igor.
    Igor: Hi Nour!
    Igor: You look different!
    Nour: Yup.
    Igor: How do you feel about it?
    -> Frazzled.
        Nour: Honestly kind of frazzled.
        Igor: Yeah...
        <<ChangeStartNode Igor Igor3>>
    -> Excited.
        Nour: Honestly I'm kind of excited.
        Nour: It's weird, but I've never been able to move this way before!
        Igor: That's great!
        Igor: When you have the chance, I would like to study the changes in your body. How fast can you run? Why is only one of your leg goat-ish when you're standing still? Does it hurt when I pinch you here? Can you --
        Nour: OK OK yeah when I get the chance.
        <<ChangeStartNode Igor Igor4>>
    -> Befuddled.
        Nour: Honestly kind of befuddled.
        Igor: Yeah...
        <<ChangeStartNode Igor Igor3>>
<<endif>>
===
title: Igor2
position: 831,-31
---
<<if $mcState is "Fish">>
    Igor: Do you need help... getting back up?
    Nour: You know what... maybe? We can try.
    Igor: Ok... Um...
    Nour: ...
    Igor: I'm sorry Nour, you're awfully slippery. I don't think this is gonna work.
    Nour: That's ok, thanks for trying...
    Nour: I think I'm heading to the hospital, I'm sure they can fix me.
    Igor: Yeah!
    <<ChangeStartNode Igor Igor3>>
<<else>>
<<endif>>
===
title: Igor3
position: 831,-31
---
<<if $mcState is "Fish">>
    Igor: You know Nour, I'm sure you'll be fine.
    Nour: Yeah?
    Igor: Yeah! You remember when my horns first sprouted?
    Nour: Yeah?
    Igor: And you know how they made the doors of the school horizontal just so I could fit in?
    Nour: That was why they did it?
    Igor: Yeah!
    Nour: That's cool...
    Igor: But?
    Nour: I just don't want people to have to... change things just for me.
    Igor: But people want to help!
    Nour: ...
    Nour: I guess so.
    <<ChangeStartNode Igor Igor4>>
<<else>>

<<endif>>
===
title: Igor4
position: 831,-31
---
<<if visited("Igor4") is false>>
    Nour: So, I didn't see you at the dance.
    Igor: Yeah.
    Nour: Wanna talk about it?
    Igor: Oh, I just feel like we're sort of grown up for that kind of things. At least I feel that way. You know I'm training to be a mad scientist?
    Nour: Oh! that sounds really mature, Igor!
    Igor: Thanks! I'm really excited! My research focus is going to be extracting the remnants of souls from cicada shells and merging them into one perfect being that doesn't feel pain!
    Nour: That's really cool!
    Nour: ...But you know it would've also been cool to hang out with you and Nizar at the dance!
    Igor: I'm sorry Nour, that sort of thing is really juvenile for me. But maybe we can go birdwatching together sometime!
    Nour: I'd like that!
<<else>>
    Nour: By the way, why only cicada shells? Why not real cicadas?
    Igor: That would be highly unethical, Nour.
    Nour: Oh. Of course.
<<endif>>
===